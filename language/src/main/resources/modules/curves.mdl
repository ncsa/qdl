module[
   'qdl:/math/curves','curves'
]body[
   » This module defines several additional functions for mathematics
   » that allow for plotting planar curves in various coordinate systems.
   » It also defines several coordinate systems for plotting, so you can just
   » pass your favorite equation in to get plotted.
   » E.g. rho(t.)->t. passed to polar_plot would plot a spiral.

 polar_plot(@rho(), interval.)->
      block[
       » Plot a function rho(theta) over the interval using polar coordinates.
       » The coordinate system is made of lines and concentric circles.
       pplot(x(t.)->rho(t.)*cos(t.),y(t.)->rho(t.)*sin(t.), interval.);
       ];

  elliptic_plot(@mu(), interval.)->
       block[
        » Plots a function 0 < mu(nu) in elliptic coordinates.
        » The coordinate curves are ellipses. Try plotting a
        » constant function.
        pplot(x(t.)->cosh(mu(t.))*cos(t.),y(t.)->sinh(mu(t.))*sin(t.), interval.);
        ];

 parabolic_plot(@sigma(), interval.)->
        block[
         » Plots a function sigma(tau) in parabolic coordinates.
         » The coordinate lines are confocal parabolas.
         pplot(x(t.)->sigma(t.)*t.,y(t.)->(t.^2 - sigma(t.)^2)/2, interval.);
         ];

  rose(a,n, interval.)->
       block[
        » Plot a rose of width 0 < a and n petals.
        » If n is rational it will be closed.
        polar_plot(rho(t.)->a*sin(n*t.), interval.);
        ];

  hypocycloid(a, b, interval.)->
      block[
       » The hypocycloid is formed when a circle of radius b rolls inside a circle of radius a.
       » b < a is common. If k:=a/b is an integer, then the curve will have k cusps. If k is
       » irrational, the curve will be space-filling.
       » An interval of [0,2*pi()] gives the complete curve in the rational k case.
       pplot(x(t.)->(a-b)*cos(t.) + b*cos((a-b)/b*t.),y(t.)->(a-b)*sin(t.) - b*sin((a-b)/b*t.),interval.);
       ];

  epicycloid(a, b, interval.)->
       block[
       » The epicycloid is formed when a circle of radius b rolls outside a circle of radius a.
       » b <= a is common. If k:=a/b is an integer, then the curve will have k cusps. If k is
       » irrational, the curve will be space-filling.
       » An interval of [0,2*pi()] gives the complete curve in the rational k case.
       » A special case is a == b, which is the cardiod.
       pplot(x(t.)->(a+b)*cos(t.) - b*cos((a+b)/b*t.),y(t.)->(a+b)*sin(t.) - b*sin((a+b)/b*t.),interval.);
       ];

  tractrix(a, interval.)->
      block[
       » The tractrix (or "Hundekurve" in German) is formed by pulling a point
       » at the end of a segment.
       » The parameter a is simply a scale factor and can usually be set to 1.
       » The interval is (-pi()/2, pi()/2) (no endpoints, since it is infinite there).
       » The German name means "dog curve" and refers to the path an unwilling
       » dog at the end of a leash makes as you drag him for a walk.
       pplot(x(t.)->a*(t. -tanh(t.)),y(t.)->a*sech(t.),interval.);
       ];

  cissoid(a, interval.)->
  block[
       » This is the Cissoid of Diocles.
       » The interval is (-pi()/2, pi()/2) ( so no endpoints, since the curve is infinite there).
       » The parameter a is a scaling factor and can usually be set to 1.
       polar_plot(r(t.)->2*a*(sec(t.)-cos(t.)), interval.);
       ];
/*
E.g.
    module_load('/home/ncsa/dev/ncsa-git/qdl/language/src/main/resources/modules/curves.mdl')
    module_import('qdl:/math/curves')
    r(t.)->1
    polar_plot(@r(), ⟦0;2*pi();20⟧)
    rose(1,3,⟦0;2*pi();2000⟧);
    s:=date_ms();x.:=rose(1,3,⟦0;2*pi();200⟧);date_ms()-s;

*/
]; //end module